plugins {
    alias(libs.plugins.kotlin.android)
}
apply plugin: 'com.android.application'
apply plugin: 'maven-publish'

android {
    namespace 'ai.elimu.herufi'
    compileSdk 35

    defaultConfig {
        applicationId "ai.elimu.herufi"
        minSdkVersion 26
        targetSdkVersion 35
        versionCode 1000006
        versionName "1.0.6-SNAPSHOT"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders = [contentProviderApplicationId: "ai.elimu.content_provider.debug", analyticsApplicationId: "ai.elimu.analytics.debug"]
            buildConfigField("String", "CONTENT_PROVIDER_APPLICATION_ID", '"ai.elimu.content_provider.debug"')
            buildConfigField("String", "ANALYTICS_APPLICATION_ID", '"ai.elimu.analytics.debug"')
        }
        release {
            minifyEnabled false
            manifestPlaceholders = [contentProviderApplicationId: "ai.elimu.content_provider", analyticsApplicationId: "ai.elimu.analytics"]
            buildConfigField("String", "CONTENT_PROVIDER_APPLICATION_ID", '"ai.elimu.content_provider"')
            buildConfigField("String", "ANALYTICS_APPLICATION_ID", '"ai.elimu.analytics"')
        }
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation libs.elimu.model // https://jitpack.io/#ai.elimu/model
    implementation libs.elimu.content.provider // https://jitpack.io/#ai.elimu/content-provider
    implementation libs.elimu.analytics // https://jitpack.io/#ai.elimu/analytics
    implementation libs.elimu.common.utils

    implementation libs.androidx.appcompat
    implementation libs.androidx.constraint.layout

    implementation libs.material
    implementation libs.flexbox
    implementation libs.androidx.core.ktx

    testImplementation libs.junit

    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso
}

publishing {
    publications {
        apk(MavenPublication) {
            artifact("${buildDir}/outputs/apk/release/app-release-unsigned.apk")
        }
    }
}
tasks.named("publishApkPublicationToMavenLocal") {
    mustRunAfter(":app:assembleRelease")
}
